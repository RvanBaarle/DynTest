module main

imports
  signatures/start-sig

signature
  sorts PATH LABEL SCOPE
  constructors
    PathStep : PATH * LABEL * SCOPE -> PATH
    PathEmpty : SCOPE -> PATH
    Label : string -> LABEL
    Scope : string -> SCOPE


primitives
  statement #print(@value)
  statement #exit()
  expression #int-add(@value, @value)
  expression #int-sub(@value, @value)
  expression #int-mul(@value, @value)
  expression #int-div(@value, @value)
  expression #record-new()
  expression #record-read(@value, @value)
  expression #record-write(@value, @value, @value)
  conditional #int-eq(@value, @value)

constraint-analyzer
  property ref :: 'ID
  property path :: 'PATH

signature
constructors
  String : STRING -> Value

rules
  $exit :: @value
  $scope :: @value

  evalProgram :: 'Start -> Holy

  evalStatements :: List('Statement) -> Holy

  evalStatement :: 'Statement -> Holy

  evalExpression :: 'Expression -> Holy(@value)

  evalExpressions :: List('Expression) -> Holy(List(@value))

  makeParamVars :: List('ID) -> List(@value)

  evalParameters :: List('ID) * List(@value) -> Holy

  evalJumpLabels :: List('Statement) -> Holy

  evalCondition :: 'Condition * @value * @value -> @statement

  evalElse :: 'Else * @value -> Holy(@value)
  
  evalPath :: @value * 'PATH -> Holy(@value)
  
  unpackPath :: List('PATH) -> 'PATH

  evalProgram(Program(l)) = {
    s <- #record-new()
    with $exit = exit, $scope = s do {
      evalJumpLabels(l)
      evalStatements(l)
      $exit@([])
    }
  }
  label exit/0:

  evalJumpLabels([JumpLabel(l)|sts]) = {
    v <- var(l)
    evalJumpLabels(sts)
    fix {
      fun v([]) = {
        evalStatements(sts)
        $exit@([])
      }
    }
    #record-write($scope, str(l), v)
  }
  evalJumpLabels([_|sts]) = evalJumpLabels(sts)
  evalJumpLabels([]) = hole

  evalStatements([a|b]) = {
    evalStatement(a)
    evalStatements(b)
  }

  evalStatements([JumpLabel(l)|_]) = {
    v <- var(l)
    v@([])
  }
  label noop/0:

  evalStatements([]) = hole

  evalStatement(Print(e)) = {
    x <- evalExpression(e)
    #print(x)
  }

  evalStatement(Simple(e)) = {
    evalExpression(e)
  }

  evalStatement(Assign(Variablee(_, x), e)) = {
    v <- evalExpression(e)
    s <- evalPath($scope, unpackPath(path(x)))
    #record-write(s, str(x), v)
  }

  evalStatements([Jump(x)|_]) = {
    s <- evalPath($scope, unpackPath(path(x)))
    lab <- #record-read(s, str(x))
    lab@([])
  }
  label noop/0:

  evalStatement(Jump(x)) = {
    s <- evalPath($scope, unpackPath(path(x)))
    lab <- #record-read(s, str(x))
    lab@([])
  }
  label noop/0:

  evalStatement(If(c, st, e)) = {
    true <- fresh-var(true)
    false <- evalElse(e, cont)
    fix {
      fun true([]) = {
        evalStatements(st)
        cont@([])
      }
    }
    evalCondition(c, true, false)
  } label cont/0:

  evalCondition(Zero(e), true, false) = {
    v <- evalExpression(e)
    if #int-eq(v, int('0)) then {
      true@([])
    } else {
      false@([])
    }
  }

  evalElse(Elif(c, st, e), cont) = {
    branch <- fresh-var(elif)
    true <- fresh-var(true)
    false <- evalElse(e, cont)
    fix {
      fun true([]) = {
        evalStatements(st)
        cont@([])
      }
      fun branch([]) = {
        evalCondition(c, true, false)
      }
    }
    branch
  }
  evalElse(Else(st), cont) = {
    branch <- fresh-var(el)
    fix {
      fun branch([]) = {
        evalStatements(st)
        cont@([])
      }
    }
    branch
  }
  evalElse(NoElse(), cont) = cont

  evalStatement(VarScope(stmts)) = {
    s <- #record-new()
    #record-write(s, str('"main!P"), $scope)
    with $scope = s do {
      evalJumpLabels(stmts)
      evalStatements(stmts)
    }
    hole
  }

  evalExpression(Value(Int(i))) = int(i)

  evalExpression(Value(String(x))) = str(x)

  evalExpression(Variable(Variablee(_, x))) = {
    s <- evalPath($scope, unpackPath(path(x)))
    #record-read(s, str(x))
  }
  
  evalPath(s, PathStep(p, Label(l), _)) = {
    s_parent <- #record-read(s, str(l))
    evalPath(s_parent, p)
  }
  evalPath(s, PathEmpty(_)) = s

  evalExpression(Add(e1, e2)) = {
    v1 <- evalExpression(e1)
    v2 <- evalExpression(e2)
    #int-add(v1, v2)
  }

  evalExpression(Sub(e1, e2)) = {
    v1 <- evalExpression(e1)
    v2 <- evalExpression(e2)
    #int-sub(v1, v2)
  }

  evalExpression(Mul(e1, e2)) = {
    v1 <- evalExpression(e1)
    v2 <- evalExpression(e2)
    #int-mul(v1, v2)
  }

  evalExpression(Div(e1, e2)) = {
    v1 <- evalExpression(e1)
    v2 <- evalExpression(e2)
    #int-div(v1, v2)
  }

  evalExpression(Lambda(paramNames, e)) = {
    lambda <- fresh-var(lambda)
    tail <- fresh-var(tail)
    params <- makeParamVars(paramNames)
    fix {
      fun lambda(params ++ [tail]) = {
        s <- #record-new()
        #record-write(s, str('"main!P"), $scope)
        with $scope = s do {
	        evalParameters(paramNames, params)
	        r <- evalExpression(e)
	        tail@([r])
        }
      }
    }
    lambda
  }

  makeParamVars([h|t]) = [fresh-var(p)|makeParamVars(t)]
  makeParamVars([]) = []

  evalParameters([name|nl], [param|pl]) = {
    #record-write($scope, str(name), param)
    evalParameters(nl, pl)
  }

  evalParameters([], []) = hole

  evalExpression(Apply(e1, es)) = {
    f <- evalExpression(e1)
    arg <- evalExpressions(es)

    f@(arg ++ [ret])
    label ret/1:
  }

  evalExpressions([e| es]) = {
    v1 <- evalExpression(e)
    vs <- evalExpressions(es)
    [v1|vs]
  }
  evalExpressions([]) = []
  
  unpackPath([p]) = p

